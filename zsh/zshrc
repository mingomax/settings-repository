# ========= P10k instant prompt (deixe no topo) =========
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
    source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# ========= Detecção de Ambiente =========
if [ -d "$HOME/Workspaces/Personal/dotfiles" ]; then
  DOTFILES_DIR="$HOME/Workspaces/Personal/dotfiles"
elif [ -d "$HOME/dotfiles" ]; then
  DOTFILES_DIR="$HOME/dotfiles"
fi

# Configurar diretórios de trabalho (com varáveis de ambiente para override)
export WORK_DIR="${WORK_DIR:-$HOME/Workspaces/Work}"
export PERSONAL_DIR="${PERSONAL_DIR:-$HOME/Workspaces/Personal}"
export TOOLS_DIR="${TOOLS_DIR:-$HOME/Tools}"

# ========= PATH básico =========
export PATH="$HOME/bin:$HOME/.local/bin:/usr/local/bin:$PATH"

# ========= Oh My Zsh =========
export ZSH="$HOME/.oh-my-zsh"
export ZSH_CUSTOM="${ZSH_CUSTOM:-$ZSH/custom}"

if [[ -d "$ZSH_CUSTOM/themes/powerlevel10k" ]]; then
  ZSH_THEME="powerlevel10k/powerlevel10k"
else
  ZSH_THEME="robbyrussell"
fi

# Se aparecer warning de compfix, descomente:
# ZSH_DISABLE_COMPFIX="true"

plugins=(
  git
  docker docker-compose
  kubectl
  colored-man-pages
  command-not-found
  history-substring-search
)

if [[ -d "$ZSH_CUSTOM/plugins/zsh-autosuggestions" ]]; then
  plugins+=(zsh-autosuggestions)
fi

if [[ -d "$ZSH_CUSTOM/plugins/zsh-completions" ]]; then
  plugins+=(zsh-completions)
fi

if [[ -d "$ZSH_CUSTOM/plugins/zsh-syntax-highlighting" ]]; then
  plugins+=(zsh-syntax-highlighting)
fi

source "$ZSH/oh-my-zsh.sh"

# ========= Preferências gerais =========
ENABLE_CORRECTION="true"
export PROMPT_DIRTRIM=3

# Configurar locale (pt_BR se disponível, senão UTF-8)
# Nota: Locale é configurado no setup.sh, isso é apenas fallback
if [ -z "${LANG:-}" ] || [ "$LANG" = "C" ]; then
  if locale -a 2>/dev/null | grep -q 'pt_BR\.utf-?8'; then
    export LANG=pt_BR.UTF-8
    export LC_ALL=pt_BR.UTF-8
  else
    export LANG=C.UTF-8
    export LC_ALL=C.UTF-8
  fi
fi

export EDITOR="${EDITOR:-nano}"
[ -x /usr/bin/code ] && export EDITOR="code -w"
[ -x /usr/bin/code-insiders ] && export EDITOR="code-insiders -w"

# ========= Java / Maven =========
# Detectar JAVA_HOME dinamicamente (compatível com diferentes versões e arquiteturas)
if [ -z "${JAVA_HOME:-}" ]; then
  # Tentar usar a versão default do sistema
  if command -v java >/dev/null 2>&1 && command -v readlink >/dev/null 2>&1; then
    JAVA_BIN=$(readlink -f "$(command -v java)")
    JAVA_HOME="${JAVA_BIN%/*/*/*}"  # Remove /bin/java
  elif [ -d "/usr/lib/jvm" ]; then
    # Fallback: procurar instalação mais recente
    JAVA_HOME=$(ls -d /usr/lib/jvm/java-*-openjdk* 2>/dev/null | head -1)
  fi
fi

if [ -n "${JAVA_HOME:-}" ] && [ ! "${JAVA_HOME}" = "$(echo :$PATH: | grep -o :${JAVA_HOME}/bin:)" ]; then
  export JAVA_HOME
  export PATH="$JAVA_HOME/bin:$PATH"
fi

# ========= Node/NPM/NVM =========
export NVM_DIR="$HOME/.nvm"
[ -s "$NVM_DIR/nvm.sh" ] && . "$NVM_DIR/nvm.sh"

# ========= fzf / bat / fd =========
# Detectar fd ou fdfind (conforme disponível no sistema)
if command -v fdfind >/dev/null 2>&1; then
  FD_CMD='fdfind'
elif command -v fd >/dev/null 2>&1; then
  FD_CMD='fd'
else
  FD_CMD=''
fi

if [ -n "$FD_CMD" ]; then
  export FZF_DEFAULT_COMMAND="$FD_CMD --hidden --follow --exclude .git"
  export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
fi

export FZF_DEFAULT_OPTS='--height=40% --border --inline-info'

# bat para pager (se disponível)
if command -v bat >/dev/null 2>&1; then
  export PAGER="bat"
  export MANPAGER="sh -c 'col -bx | bat -l man -p'"
elif command -v less >/dev/null 2>&1; then
  export PAGER="less"
fi

# ========= zoxide =========
if command -v zoxide >/dev/null 2>&1; then
    eval "$(zoxide init zsh)"
fi

# ========= direnv =========
if command -v direnv >/dev/null 2>&1; then
  eval "$(direnv hook zsh)"
fi

# ========= Histórico =========
HISTFILE=~/.zsh_history
HISTSIZE=200000
SAVEHIST=200000
setopt HIST_IGNORE_DUPS HIST_IGNORE_ALL_DUPS HIST_REDUCE_BLANKS \
    EXTENDED_HISTORY SHARE_HISTORY INC_APPEND_HISTORY
bindkey '^[[A' history-substring-search-up
bindkey '^[[B' history-substring-search-down

# ========= Qualidade de vida =========
setopt autocd auto_pushd pushd_silent pushd_ignore_dups
setopt correct
setopt interactivecomments
unsetopt beep

# ========= Aliases =========
# Usar DOTFILES_DIR detectado anteriormente
if [ -n "${DOTFILES_DIR:-}" ] && [ -f "$DOTFILES_DIR/zsh/aliases.zsh" ]; then
  source "$DOTFILES_DIR/zsh/aliases.zsh"
fi

# ========= Git aliases =========
if [ -n "${DOTFILES_DIR:-}" ] && [ -f "$DOTFILES_DIR/git/aliases.zsh" ]; then
  source "$DOTFILES_DIR/git/aliases.zsh"
fi

# ========= Powerlevel10k (config do prompt) =========
[[ -r ~/.p10k.zsh ]] && source ~/.p10k.zsh

#THIS MUST BE AT THE END OF THE FILE FOR SDKMAN TO WORK!!!
export SDKMAN_DIR="$HOME/.sdkman"
[[ -s "$HOME/.sdkman/bin/sdkman-init.sh" ]] && source "$HOME/.sdkman/bin/sdkman-init.sh"

# ========= SSH Agent Auto Start =========
# Carrega env criado pelo ssh-agent-start (direnv ou zsh farão o source)
ENVFILE="$HOME/.ssh/agent.env"
[ -f "$ENVFILE" ] && source "$ENVFILE"

# Se não houver socket válido, execute o script (ele cria/atualiza ~/.ssh/agent.env)
if [ -x "$HOME/.local/bin/ssh-agent-start" ] && { [ -z "${SSH_AUTH_SOCK:-}" ] || [ ! -S "${SSH_AUTH_SOCK:-}" ]; }; then
  "$HOME/.local/bin/ssh-agent-start" >/dev/null 2>&1 || true
  [ -f "$ENVFILE" ] && source "$ENVFILE"
fi
